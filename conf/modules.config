/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withName: GUNZIP_GTF {
        publishDir = [
            enabled: false
        ]
    }

    withName: GTF_TO_TABLE {
        publishDir = [
            [
                path: { "${params.outdir}/tables/annotation" },
                mode: params.publish_dir_mode,
                pattern: '*.anno.tsv'
            ]
        ]
        ext.args = "--feature-type '${params.features_gtf_feature_type}' --first-field '${params.features_gtf_table_first_field}'"
    }

    withName: VALIDATOR {
        publishDir = [
            enabled: false
        ]
        ext.args = "--sample_id_col '${params.observations_id_col}' --feature_id_col '${params.features_id_col}'"
    }

    withName: AFFY_JUSTRMA_RAW {
        publishDir = [
            [
                path: { "${params.outdir}/tables/processed_abundance" },
                mode: params.publish_dir_mode,
                pattern: '*.matrix.tsv'
            ],
            [
                path: { "${params.outdir}/tables/annotation" },
                mode: params.publish_dir_mode,
                pattern: '*.annotation.tsv'
            ],
            [
                path: { "${params.outdir}/other/affy" },
                mode: params.publish_dir_mode,
                pattern: '*.{rds,sessionInfo.log}'
            ]
        ]
        ext.prefix = { "raw." }
        ext.args = { [
            "--sample_name_col \"${params.observations_name_col}\"",
            "--file_name_col \"${params.affy_file_name_col}\"",
            "--background ${params.affy_background}",
            "--normalize False",
            "--bgversion ${params.affy_bgversion}",
            "--file_name_col \"${params.affy_file_name_col}\"",
            "--destructive ${params.affy_destructive}",
            "--rm.mask ${params.affy_rm_mask}",
            "--rm.outliers ${params.affy_rm_outliers}",
            "--rm.extra ${params.affy_rm_extra}",
            "--build_annotation ${params.affy_build_annotation}"
        ].join(' ').trim() }
    }

    withName: AFFY_JUSTRMA_NORM {
        publishDir = [
            [
                path: { "${params.outdir}/tables/processed_abundance" },
                mode: params.publish_dir_mode,
                pattern: '*.matrix.tsv'
            ],
        ]
        ext.prefix = { "normalised." }
        ext.args = { [
            "--sample_name_col \"${params.observations_name_col}\"",
            "--file_name_col \"${params.affy_file_name_col}\"",
            "--background ${params.affy_background}",
            "--normalize True",
            "--bgversion ${params.affy_bgversion}",
            "--file_name_col \"${params.affy_file_name_col}\"",
            "--destructive ${params.affy_destructive}",
            "--rm.mask ${params.affy_rm_mask}",
            "--rm.outliers ${params.affy_rm_outliers}",
            "--rm.extra ${params.affy_rm_extra}",
            "--build_annotation ${params.affy_build_annotation}"
        ].join(' ').trim() }
    }

    withName:  READ_FROM_SOFT {
        publishDir = [
            [
                path: { "${params.outdir}/tables/processed_abundance" },
                mode: params.publish_dir_mode,
                pattern: '*.matrix*.tsv'
            ],
            [
                path: { "${params.outdir}/tables/annotation" },
                mode: params.publish_dir_mode,
                pattern: '*.annotation.tsv'
            ],
            [
                path: { "${params.outdir}/other/affy" },
                mode: params.publish_dir_mode,
                pattern: '*.{rds,sessionInfo.log}'
            ]
        ]
        ext.prefix = { "normalised." }
        ext.args = {[
            "--querygse \"${params.querygse}\"",
            "-- metacols \"${params.features_metadata_cols}\"",
            "-- features_id_col \"${params.features_id_col}\""
            ].join(' ').trim() }
    }

    withName: DESEQ2_DIFFERENTIAL {
        publishDir = [
            [
                path: { "${params.outdir}/tables/differential" },
                mode: params.publish_dir_mode,
                pattern: '*.deseq2.results.tsv'
            ],
            [
                path: { "${params.outdir}/tables/differential" },
                mode: params.publish_dir_mode,
                pattern: '*.deseq2.results.annotated.tsv'
            ],
            [
                path: { "${params.outdir}/tables/processed_abundance" },
                mode: params.publish_dir_mode,
                pattern: '*.{normalised_counts,vst,rlog}.tsv'
            ],
            [
                path: { "${params.outdir}/plots/qc" },
                mode: params.publish_dir_mode,
                pattern: '*.png'
            ],
            [
                path: { "${params.outdir}/other/deseq2" },
                mode: params.publish_dir_mode,
                pattern: '*.{rds,sizefactors.tsv,sessionInfo.log}'
            ]
        ]
        ext.args = { [
            "--gene_id_col \"${params.features_id_col}\"",
            "--sample_id_col \"${params.observations_id_col}\"",
            "--features_name_col \"${params.features_name_col}\"",
            "--test $params.deseq2_test",
            "--fit_type $params.deseq2_fit_type",
            "--sizefactors_from_controls $params.sizefactors_from_controls",
            "--sf_type $params.deseq2_sf_type",
            "--min_replicates_for_replace $params.deseq2_min_replicates_for_replace",
            "--use_t $params.deseq2_use_t",
            "--lfc_threshold $params.deseq2_lfc_threshold",
            "--alt_hypothesis $params.deseq2_alt_hypothesis",
            "--independent_filtering $params.deseq2_independent_filtering",
            "--p_adjust_method $params.deseq2_p_adjust_method",
            "--alpha $params.deseq2_alpha",
            "--minmu $params.deseq2_minmu",
            "--vs_method $params.deseq2_vs_method",
            "--vs_blind $params.deseq2_vs_blind",
            "--vst_nsub $params.deseq2_vst_nsub",
            "--shrink_lfc $params.deseq2_shrink_lfc",
            "--cores $params.deseq2_cores",
            "--subset_to_contrast_samples $params.differential_subset_to_contrast_samples",
            ((meta.blocking == null) ? '' : "--blocking_variables $meta.blocking"),
            ((meta.exclude_samples_col == null) ? '' : "--exclude_samples_col $meta.exclude_samples_col"),
            ((meta.exclude_samples_values == null) ? '' : "--exclude_samples_values $meta.exclude_samples_values")
        ].join(' ').trim() }
    }

    withName: LIMMA_DIFFERENTIAL {
        publishDir = [
            [
                path: { "${params.outdir}/tables/differential" },
                mode: params.publish_dir_mode,
                pattern: '*.limma.results.tsv'
            ],
            [
                path: { "${params.outdir}/tables/differential" },
                mode: params.publish_dir_mode,
                pattern: '*.limma.results.annotated.tsv'
            ],
            [
                path: { "${params.outdir}/plots/qc" },
                mode: params.publish_dir_mode,
                pattern: '*.png'
            ],
            [
                path: { "${params.outdir}/other/limma" },
                mode: params.publish_dir_mode,
                pattern: '*.{rds,sessionInfo.log}'
            ]
        ]
        ext.args = { [
            "--probe_id_col \"${params.features_id_col}\"",
            "--sample_id_col \"${params.observations_id_col}\"",
            "--ndups \"${params.limma_ndups}\"",
            "--spacing \"${params.limma_spacing}\"",
            "--block \"${params.limma_block}\"",
            "--correlation ${params.limma_correlation}",
            "--method \"${params.limma_method}\"",
            "--proportion ${params.limma_proportion}",
            "--stdev.coef.lim \"${params.limma_stdev_coef_lim}\"",
            "--trend ${params.limma_trend}",
            "--robust ${params.limma_robust}",
            "--winsor.tail.p \"${params.limma_winsor_tail_p}\"",
            "--p.value ${params.limma_p_value}",
            "--lfc ${params.limma_lfc}",
            "--confint ${params.limma_confint}",
            "--subset_to_contrast_samples $params.differential_subset_to_contrast_samples",
            ((meta.blocking == null) ? '' : "--blocking_variables $meta.blocking"),
            ((meta.exclude_samples_col == null) ? '' : "--exclude_samples_col $meta.exclude_samples_col"),
            ((meta.exclude_samples_values == null) ? '' : "--exclude_samples_values $meta.exclude_samples_values")
        ].join(' ').trim() }
    }

    withName: GSEA_GSEA {
        ext.prefix = { "${meta.id}.${gene_sets.baseName}." }
        publishDir = [
            [
                path: { "${params.outdir}/tables/gsea/${meta.id}/${gene_sets.baseName}" },
                mode: params.publish_dir_mode,
                pattern: '*gsea_report_for_*.tsv'
            ],
            [
                path: { "${params.outdir}/plots/gsea/${meta.id}/${gene_sets.baseName}" },
                mode: params.publish_dir_mode,
                pattern: '*.png'
            ]
        ]
        ext.args = { [
            "-nperm $params.gsea_nperm",
            "-permute $params.gsea_permute",
            "-scoring_scheme $params.gsea_scoring_scheme",
            "-metric $params.gsea_metric",
            "-sort $params.gsea_sort",
            "-order $params.gsea_order",
            "-set_max $params.gsea_set_max",
            "-set_min $params.gsea_set_min",
            "-norm $params.gsea_norm",
            "-rnd_type $params.gsea_rnd_type",
            "-make_sets $params.gsea_make_sets",
            "-median $params.gsea_median",
            "-num $params.gsea_num",
            "-plot_top_x $params.gsea_plot_top_x",
            "-rnd_seed $params.gsea_rnd_seed",
            "-save_rnd_lists $params.gsea_save_rnd_lists",
            "-zip_report $params.gsea_zip_report"
        ].join(' ').trim() }
    }

    withName: PLOT_EXPLORATORY {
        publishDir = [
            path: { "${params.outdir}/plots/exploratory" },
            mode: params.publish_dir_mode,
        ]
        memory = { check_max( 12.GB * task.attempt, 'memory'  ) }
        ext.args = { [
            "--sample_id_col \"${params.observations_id_col}\"",
            "--feature_id_col \"${params.features_id_col}\"",
            "--assay_names \"${params.exploratory_assay_names}\"",
            "--final_assay \"${params.exploratory_final_assay}\"",
            "--outlier_mad_threshold ${params.exploratory_mad_threshold}",
            "--palette_name \"${params.exploratory_palette_name}\""
        ].join(' ').trim() }
    }

    withName: PLOT_DIFFERENTIAL {
        publishDir = [
            path: { "${params.outdir}/plots/differential" },
            mode: params.publish_dir_mode,
        ]
        memory = { check_max( 12.GB * task.attempt, 'memory'  ) }
        ext.args = { [
            "--feature_id_col \"${params.features_id_col}\"",
            "--reference_level \"$meta.reference\"",
            "--treatment_level \"$meta.target\"",
            "--fold_change_col \"${params.differential_fc_column}\"",
            "--p_value_column \"${params.differential_qval_column}\"",
            "--diff_feature_id_col \"${params.differential_feature_id_column}\"",
            "--fold_change_threshold \"${params.differential_min_fold_change}\"",
            "--p_value_threshold \"${params.differential_max_qval}\"",
            "--unlog_foldchanges \"${params.differential_foldchanges_logged}\"",
            "--palette_name \"${params.differential_palette_name}\""
        ].join(' ').trim() }
    }

    withName: SHINYNGS_APP {
        secret = (params.shinyngs_deploy_to_shinyapps_io) ? [ 'SHINYAPPS_TOKEN', 'SHINYAPPS_SECRET' ]: null

        publishDir = [
            path: { "${params.outdir}/shinyngs_app" },
            mode: params.publish_dir_mode,
        ]
        memory = { check_max( 12.GB * task.attempt, 'memory'  ) }
        ext.args = { [
            "--assay_names \"${params.exploratory_assay_names}\"",
            "--sample_id_col \"${params.observations_id_col}\"",
            "--feature_id_col \"${params.features_id_col}\"",
            "--feature_name_col \"${params.features_name_col}\"",
            "--diff_feature_id_col \"${params.differential_feature_id_column}\"",
            "--fold_change_column \"${params.differential_fc_column}\"",
            "--pval_column \"${params.differential_pval_column}\"",
            "--qval_column \"${params.differential_qval_column}\"",
            "--unlog_foldchanges \"${params.differential_foldchanges_logged}\"",
            ((params.report_title == null) ? '' : "--title \"$params.report_title\""),
            ((params.report_author == null) ? '' : "--author \"$params.report_author\""),
            ((params.report_description == null) ? '' : "--description \"$params.report_description\""),
            ((params.shinyngs_guess_unlog_matrices) ? "--guess_unlog_matrices" : ''),
            ((params.shinyngs_deploy_to_shinyapps_io) ? "--deploy_app" : ''),
            ((params.shinyngs_shinyapps_account == null) ? '' : "--shinyapps_account \"$params.shinyngs_shinyapps_account\""),
            ((params.shinyngs_shinyapps_app_name == null) ? '' : "--shinyapps_name \"$params.shinyngs_shinyapps_app_name\"")
        ].join(' ').trim() }
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            pattern: '*_versions.yml'
        ]
    }

    withName: RMARKDOWNNOTEBOOK {
        conda = "bioconda::r-shinyngs=1.7.2"
        container = { "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ? 'https://depot.galaxyproject.org/singularity/r-shinyngs:1.7.2--r42hdfd78af_0' : 'quay.io/biocontainers/r-shinyngs:1.7.2--r42hdfd78af_0' }" }
        publishDir = [
            path: { "${params.outdir}/report" },
            mode: params.publish_dir_mode,
            pattern: '*.html'
        ]
    }

    withName: CUSTOM_MATRIXFILTER {
        publishDir = [
            enabled: false
        ]
        ext.prefix = { "${meta.id}" }
        ext.args   = [
            "--sample_id_col \"${params.observations_id_col}\"",
            "--minimum_samples ${params.filtering_min_samples}",
            "--minimum_abundance ${params.filtering_min_abundance}",
            (params.filtering_min_proportion ? "--minimum_proportion ${params.filtering_min_proportion}" : ''),
            (params.filtering_grouping_var ? "--grouping_variable \"${params.filtering_grouping_var}\"" : '')
        ].join(' ').trim()
    }

    withName: 'CUSTOM_TABULARTOGSEACLS' {
        publishDir = [
            enabled: false
        ]
        ext.args = { [ "separator": "\t", "variable": "$meta.variable" ] }
    }

    withName: 'CUSTOM_TABULARTOGSEAGCT' {
        publishDir = [
            enabled: false
        ]
    }

    withName: 'TABULAR_TO_GSEA_CHIP' {
        publishDir = [
            enabled: false
        ]
    }
}
